import com.android.build.gradle.tasks.MergeResources

plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "org.jlleitschuh.gradle.ktlint" version "$ktlint_plugin_version"
    id 'checkstyle' // only needed for Java code
}

checkstyle {
    configFile = new File('../config/checkstyle/checkstyle.xml')
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"

    namespace "org.briarproject.mailbox"

    defaultConfig {
        applicationId "org.briarproject.mailbox"
        minSdkVersion 16
        targetSdkVersion 33
        versionCode 10007
        versionName "1.0.7"
        multiDexEnabled true // only needed when minSdkVersion < 21
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            shrinkResources false
            minifyEnabled true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources false
            minifyEnabled true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/*'
        // Due to https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        exclude 'META-INF/licenses/*'
        exclude '**/attach_hotspot_windows.dll'
    }
    lintOptions {
        warning 'MissingTranslation'
        warning 'ExtraTranslation'
    }
}

configurations {
    tor
}

dependencies {
    implementation project(path: ':mailbox-core', configuration: 'default')
    implementation 'org.briarproject:dont-kill-me-lib:0.2.7'
    implementation "org.briarproject:onionwrapper-android:$onionwrapper_version"

    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "androidx.fragment:fragment-ktx:1.5.5"
    implementation 'androidx.preference:preference-ktx:1.2.0'

    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.8.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Java 8
    // Note: cannot upgrade to 2.0.0 yet as we need to use the Android Gradle plugin 7.4.0+ then
    // which is still in alpha.
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
    // Multidex needed if we target API < 21
    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"

    tor "org.briarproject:tor-android:$tor_version"
    tor "org.briarproject:obfs4proxy-android:$obfs4_version"
    tor "org.briarproject:snowflake-android:$snowflake_version"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    constraints {
        // Work around duplicate class issues caused by dont-kill-me-lib, this project, and the
        // IDEA plugin using different versions of Kotlin
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0'
    }
}

def torLibsDir = 'src/main/jniLibs'

task cleanTorBinaries {
    doLast {
        delete fileTree(torLibsDir)
    }
}

clean.dependsOn cleanTorBinaries

task unpackTorBinaries {
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torLibsDir
        }
    }
    dependsOn cleanTorBinaries
}

tasks.withType(MergeResources) {
    inputs.dir torLibsDir
    dependsOn unpackTorBinaries
}

apply from: "../gradle/ktlint.gradle"
apply from: "../gradle/checkstyle.gradle"
